#
# A simple theme based on polly theme
#

# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white

# Load dependencies.
pmodload 'helper'

prompt_polly_git_info() {
  if (( _prompt_polly_precmd_async_pid > 0 )); then
    # Append Git status.
    if [[ -s "$_prompt_polly_precmd_async_data" ]]; then
      alias typeset='typeset -g'
      source "$_prompt_polly_precmd_async_data"
      unalias typeset
    fi

    # Reset PID.
    _prompt_polly_precmd_async_pid=0

    # Redisplay prompt.
    zle && zle reset-prompt
  fi
}

function prompt_polly_precmd_async {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
    ### TODO XXX
    # This section exists to patch over vulnerabilities when sourcing the
    # file in $_prompt_polly_precmd_async_data. Without it if a branch is named
    # $foo it will expand if we have a $foo variable, and a branch named
    # $(IFS=_;cmd=rm_-rf_~;$cmd) could delete the users home directory.
    # This is a stopgap to prevent code execution and fix the vulnerability,
    # but it eventually needs to be removed in favor of zsh_async and not using
    # a file to store the prompt data in.
    ###
    local tmp_prompt_var=$(typeset -p git_info)
    # Replace all $ with $\ to escape
    tmp_prompt_var=${tmp_prompt_var//\$/\\$}
    # Unescape the first \$ as it's our $( )
    tmp_prompt_var=${tmp_prompt_var:s/\\$/\$}
    # Escape all backticks ` to \`
    tmp_prompt_var=${tmp_prompt_var//\`/\\\`}
    printf "%s\n" "$tmp_prompt_var" >! "$_prompt_polly_precmd_async_data"
  fi

  # Signal completion to parent process.
  kill -WINCH $$
}

function prompt_polly_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  _prompt_status=" %(?:%B%K{15}%F{1} Î» %f%k%b :%B%K{1}%F{15} Î» %f%k%b) "

  # Format PWD.
  _prompt_polly_pwd=$(prompt-pwd)

  # Kill the old process of slow commands if it is still running.
  if (( _prompt_polly_precmd_async_pid > 0 )); then
    kill -KILL "$_prompt_polly_precmd_async_pid" &>/dev/null
  fi

  # Compute slow commands in the background.
  trap prompt_polly_git_info WINCH
  prompt_polly_precmd_async &!
  _prompt_polly_precmd_async_pid=$!
}

function prompt_polly_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  _prompt_polly_precmd_async_pid=0
  _prompt_polly_precmd_async_data=$(mktemp "${TMPDIR:-/tmp}/polly-prompt-async-XXXXXXXXXX")

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_polly_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{2}âº%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}â™º%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{1}âº%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{2}âœš%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}ðŸ¡…%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}ðŸ¡‡%f%%b'
  zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}â¨¯%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{4}âœ±%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}âžœ%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{6}âŠ¡%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}â•%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}?%f%%b'
  zstyle ':prezto:module:git:info:branch' format ' %%B%F{15}â€%bâ€%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'status' '$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='${_prompt_status}${SSH_TTY:+"%F{9}%n%f%F{7}@%f%F{3}%m%f "}%F{8}${_prompt_polly_pwd}%f%(!. %B%F{1}#%f%b.)${git_info:+${(e)git_info[status]}}${editor_info[keymap]} '
  RPROMPT=''
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_polly_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'polly'
}

prompt_polly_setup "$@"
